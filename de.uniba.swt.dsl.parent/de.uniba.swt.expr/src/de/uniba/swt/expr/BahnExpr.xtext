grammar de.uniba.swt.expr.BahnExpr with org.eclipse.xtext.common.Terminals

generate bahnExpr "http://www.uniba.de/swt/expr/BahnExpr"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

StatementList:
	{StatementList} (stmts+=Statement)*
;

Statement:
	SelectionStmt | IterationStmt | VarDeclStmt | AssignmentStmt
;

SelectionStmt returns Statement:
	{SelectionStmt} 'if' expr=Expression
	thenStmts=StatementList
	(=>'else'
		elseStmts=StatementList
	)?
	'end'
;

IterationStmt returns Statement:
	{IterationStmt} 'for' var=IterationVarDecl 'in' referenceExpr=ValuedReferenceExpr
		stmts=StatementList
	'end'
;

IterationVarDecl:
	type=ValueType name=ID
;

VarDeclStmt returns Statement:
	{VarDeclStmt} type=ValueType name=ID ('[' cardinality=Expression ']')? assignment=VariableAssignment
;

AssignmentStmt returns Statement:
	{AssignmentStmt} referenceExpr=ValuedReferenceExpr assignment=VariableAssignment
;

VariableAssignment:
	'=' ('{' arrExprs+=Expression (',' arrExprs+=Expression)* '}' | expr=Expression)
;

Expression returns Expression:
	LogicalOrExpr
; 

LogicalOrExpr returns Expression:
	{LogicalOrExpr}leftExpr=LogicalAndExpr (ops+=OrOp rightExprs+=LogicalAndExpr)* 	
;

LogicalAndExpr returns Expression:
	{LogicalAndExpr}leftExpr=EqualityExpr (ops+=AndOp rightExprs+=EqualityExpr)*
;

EqualityExpr returns Expression:
	{EqualityExpr}leftExpr=RelationalExpr (ops+=EqualityOp rightExprs+=RelationalExpr)*
;

RelationalExpr returns Expression:
	{RelationalExpr}leftExpr=AdditiveExpr (ops+=RelationalOp rightExprs+=AdditiveExpr)*
;

AdditiveExpr returns Expression:
	{AdditiveExpr}leftExpr=MultiplicativeExpr (ops+=AddSubOp rightExprs+=MultiplicativeExpr)*
;

MultiplicativeExpr returns Expression:
	{MultiplicativeExpr}leftExpr=UnaryExpr (ops+=MulDivOp rightExprs+=UnaryExpr)*
;

UnaryExpr returns Expression:
	{UnaryExpr}op=UnaryOp expr=UnaryExpr
	| FactorExpr
;

FactorExpr:
	ParenthesizedExpr
	| LiteralExpr
	| ValuedReferenceExpr
;

ParenthesizedExpr:
	'(' expr=Expression ')'
;

LiteralExpr:
	BooleanLiteral
	| NumberLiteral
;

ValuedReferenceExpr:
	name=ID 
		('['indices+=Expression']')* 
		('.' propRef=ValuedReferenceExpr)?
;

BooleanLiteral returns LiteralExpr:
	{BooleanLiteral} boolValue=BOOLEAN
;

NumberLiteral:
	{NumberLiteral} value=NUMBER
;

enum ValueType:
	INT_TYPE='int' | FLOAT_TYPE = 'float' | BOOLEAN_TYPE = 'bool'
;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false'
;

enum AddSubOp returns OperatorType:
	PLUS = '+' | MINUS = '-'
;

enum MulDivOp returns OperatorType:
	MULTIPLY = '*' | DIVISION = '/'
;

enum UnaryOp:
	NOT='!' | NEGATION="-"	
;

enum OrOp returns OperatorType:
	OR = '||'
;

enum AndOp returns OperatorType:
	AND = '&&'
;

enum EqualityOp returns OperatorType:
	EQUAL='==' | NOT_EQUAL='!='
;

enum RelationalOp returns OperatorType:
	GREATER='>' | LESS='<' | GREATER_OR_EQUAL='>=' | LES_OR_EQUAL='<='
;

NUMBER hidden():
	(INT ('.' INT)?)
;